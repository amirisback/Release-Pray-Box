plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
}

android {

    compileSdk projectCompileSdk

    defaultConfig {
        applicationId projectApplicationId
        minSdk projectMinSdk
        targetSdk projectTargetSdk
        versionCode projectVersionCode
        versionName projectVersionName

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Declaration apps name debug mode
        def debugAttribute = " Development"
        def nameAppDebug = nameApp + debugAttribute

        // Declaration admob id for debug
        def debugAdmobPublisherId = "ca-app-pub-3940256099942544~3347511713"
        def debugAdmobBanner = "ca-app-pub-3940256099942544/6300978111"
        def debugAdmobInterstitial = "ca-app-pub-3940256099942544/1033173712"

        // Naming APK // AAB
        setProperty("archivesBaseName", nameAPK + versionName + "#" + versionCode + "-"
                + new Date().format("ddMMyy"))
        resourceConfigurations += ['en', 'id']

        vectorDrawables.useSupportLibrary = true

        // Inject app name for debug
        resValue "string", "app_name", nameAppDebug

        // Inject admob id for debug
        resValue "string", "admob_publisher_id", debugAdmobPublisherId
        resValue "string", "admob_banner", debugAdmobBanner
        resValue "string", "admob_interstitial", debugAdmobInterstitial

    }

    signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            // [PROJECT FOLDER NAME/app/[COPY YOUT KEY STORE] .jks in here
            storeFile file("frogoboxdev.jks")
            storePassword "cronoclez"
            keyAlias "frogobox"
            keyPassword "xeonranger"
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            // Generated Signed APK / AAB
            signingConfig signingConfigs.release

            // Declaration admob id for release
            def releaseAdmobPublisherId = "ca-app-pub-2963185309167575~7208187161"
            def releaseAdmobBanner = "ca-app-pub-2963185309167575/4582023823"
            def releaseAdmobInterstitial = "ca-app-pub-2963185309167575/4900560358"

            // Inject app name for release
            resValue "string", "app_name", nameApp

            // Inject admob id for release
            resValue "string", "admob_publisher_id", releaseAdmobPublisherId
            resValue "string", "admob_banner", releaseAdmobBanner
            resValue "string", "admob_interstitial", releaseAdmobInterstitial
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
    namespace 'com.frogobox.praybox'

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"

    implementation "androidx.work:work-runtime-ktx:2.7.1"

    implementation "com.google.code.gson:gson:2.9.0"
    implementation "com.google.android.material:material:1.5.0"
    implementation "com.google.android.gms:play-services-ads:20.6.0"

    implementation "org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2"

    implementation "com.github.PhilJay:MPAndroidChart:3.1.0"

    implementation "com.github.amirisback:frogo-recycler-view:4.0.7"
    implementation "com.github.amirisback:frogo-admob:4.3.1"

    implementation "com.github.frogobox:frogo-sdk:0.0.2"
    implementation "com.github.frogobox:frogo-ui:0.0.1"
    implementation "com.github.frogobox:frogo-consume-api:2.0.5"

    implementation "com.github.bumptech.glide:glide:4.13.1"

    kapt "androidx.lifecycle:lifecycle-compiler:2.5.0-alpha05"
    kapt "com.github.bumptech.glide:compiler:4.13.1"

}