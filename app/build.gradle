plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
}

android {

    // project settings
    def nameApp = "Pray Box"

    def appDomain = "com"
    def appDevConsole = "frogobox"
    def appName = "praybox"

    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 4

    def projectCompileSdk = 33
    def projectMinSdk = 21
    def projectTargetSdk = projectCompileSdk

    def projectApplicationId = "$appDomain.$appDevConsole.$appName"
    def projectVersionCode = (versionMajor * 100) + (versionMinor * 10) + (versionPatch * 1)
    def projectVersionName = "$versionMajor.$versionMinor.$versionPatch"

    def nameAPK = nameApp.toLowerCase().replace(" ", "-")

    compileSdk projectCompileSdk
    namespace 'com.frogobox.praybox'

    defaultConfig {
        applicationId projectApplicationId
        minSdk projectMinSdk
        targetSdk projectTargetSdk
        versionCode projectVersionCode
        versionName projectVersionName

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Declaration apps name debug mode
        def debugAttribute = " Development"
        def nameAppDebug = nameApp + debugAttribute

        // Declaration admob id for debug
        def debugAdmobPublisherId = "ca-app-pub-3940256099942544~3347511713"
        def debugAdmobBanner = "ca-app-pub-3940256099942544/6300978111"
        def debugAdmobInterstitial = "ca-app-pub-3940256099942544/1033173712"

        // Naming APK // AAB
        setProperty("archivesBaseName", nameAPK + versionName + "#" + versionCode + "-"
                + new Date().format("ddMMyy"))
        resourceConfigurations += ['en', 'id']

        vectorDrawables.useSupportLibrary = true

        // Inject app name for debug
        resValue "string", "app_name", nameAppDebug

        // Inject admob id for debug
        resValue "string", "admob_publisher_id", debugAdmobPublisherId
        resValue "string", "admob_banner", debugAdmobBanner
        resValue "string", "admob_interstitial", debugAdmobInterstitial

    }

    signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            // [PROJECT FOLDER NAME/app/[COPY YOUT KEY STORE] .jks in here
            storeFile file("frogoboxdev.jks")
            storePassword "cronoclez"
            keyAlias "frogobox"
            keyPassword "xeonranger"
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            // Generated Signed APK / AAB
            signingConfig signingConfigs.release

            // Declaration admob id for release
            def releaseAdmobPublisherId = "ca-app-pub-2963185309167575~7208187161"
            def releaseAdmobBanner = "ca-app-pub-2963185309167575/4582023823"
            def releaseAdmobInterstitial = "ca-app-pub-2963185309167575/4900560358"

            // Inject app name for release
            resValue "string", "app_name", nameApp

            // Inject admob id for release
            resValue "string", "admob_publisher_id", releaseAdmobPublisherId
            resValue "string", "admob_banner", releaseAdmobBanner
            resValue "string", "admob_interstitial", releaseAdmobInterstitial
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }


}

dependencies {

    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.core:core-ktx:1.10.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    implementation "androidx.work:work-runtime-ktx:2.8.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.google.android.material:material:1.8.0"
    implementation "com.google.android.gms:play-services-ads:22.0.0"

    implementation "org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2"

    implementation "com.github.PhilJay:MPAndroidChart:3.1.0"

    implementation "com.github.amirisback:frogo-recycler-view:4.3.4"
    implementation "com.github.amirisback:frogo-admob:5.2.0"

    implementation "com.github.frogobox:frogo-sdk:2.1.6"
    implementation "com.github.frogobox:frogo-ui:1.1.4"
    implementation "com.github.frogobox:frogo-consume-api:2.4.5"

    implementation "com.github.bumptech.glide:glide:4.15.1"
    kapt "com.github.bumptech.glide:compiler:4.15.1"

}